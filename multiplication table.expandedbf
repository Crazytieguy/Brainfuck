10 minus row ; row ; rowcopy ; 10 minus col ; col ; colcopy ; 10 minus ones ; ones ; ones ascii ;
     0          1       2           3          4       5            6          7          8
10 minus tens ; tens ; tens ascii ; 10 minus hundreds ; hundreds ; hundreds ascii ; 10 minus thousands
      9          10        11              12              13            14                 15
thousands ; thousands ascii ; 10 minus 10 thousands ; 10 thousands ; 10 thousands ascii
    16             17                  18                  19                20
>> 0
>> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> 20
< +++++ + [- > +++++ +++ <] > 20
<< << +++++ +++++ << 17
< +++++ + [- > +++++ +++ <] > 17
<< << +++++ +++++ << 14
< +++++ + [- > +++++ +++ <] > 14
<< << +++++ +++++ << 11
< +++++ + [- > +++++ +++ <] > 11
<< << +++++ +++++ << 8
< +++++ + [- > +++++ +++ <] > 8
<< << +++++ +++++ << 5
<< << << << << 0
<< +++++ minus 1
[- minus 1
    >> +++++ +++++ 0
    [- 0
        >> + << 0
        < +++++ minus half
        [- > 0
            >> >> >> +++++ +++++ 3
            [- >> + 4
                << << << 1
                [->+>+<<] > [-<+>] > 2
                [- 2
                    >> >> 4
                    [->+>+<<] > [-<+>] > 5
                    [- >> 6
                        - > + < [>> + < -] > [>] < 7
                        < [- < [-] +++++ +++++ >> 7
                            [-] >> >> 9
                            - > + < [>> + < -] > [>] < 10
                            < [- < [-] +++++ +++++ >> 10
                                [-] >> >> 12
                                - > + < [>> + < -] > [>] < 13
                                < [- < [-] +++++ +++++ >> 13
                                    [-] >> >> 15
                                    - > + < [>> + < -] > [>] < 16
                                    < [- < [-] +++++ +++++ >> 16
                                        [-] >> >> 18
                                        - >> + 19
                                        << << << 16
                                    < ] > 16
                                    << << << 13
                                < ] > 13
                                << << << 10
                            < ] > 10
                            << << << 7
                        < ] > 7
                    << <<] 5
                << << <<] 2
                >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> 19
                > + < [> [-]] >> [>] << [[-] ++++ [- > ---- <] >> + << << << << + >> >> >>] < 19
                [- > + > + <<] >>. < [- > - << + >] < 19
                >>> [[-] ++++ [- < ++++ >]] <<< 19
                [-] << [-] +++++ +++++ << << 16

                [> [-]] >> [>] << [[-] ++++ [- > ---- <] >> + << << << << + >> >> >>] < 16
                [- > + > + <<] >>. < [- > - << + >] < 16
                >>> [[-] ++++ [- < ++++ >]] <<< 16
                [-] << [-] +++++ +++++ << << 13

                [> [-]] >> [>] << [[-] ++++ [- > ---- <] >> + << << << << + >> >> >>] < 13
                [- > + > + <<] >>. < [- > - << + >] < 13
                >>> [[-] ++++ [- < ++++ >]] <<< 13
                [-] << [-] +++++ +++++ << << 10

                [> [-]] >> [>] << [[-] ++++ [- > ---- <] >> + <<] < 10
                [- > + > + <<] >>. < [- > - << + >] < 10
                >>> [[-] ++++ [- < ++++ >]] <<< 10
                [-] << [-] +++++ +++++ << << 7

                [- > + > + <<] >>. < [- > - << + >] < 7
                > ++++ [- > ---- <] >. < ++++ [- > ++++ <] < 7
                [-] << [-] +++++ +++++ << << 4
            <<] 3
            << << << <] minus half
        >> >> >> > 3
        >> [-] +++++ +++++. ----- ----- << 3
    << << <<] 0
<<]